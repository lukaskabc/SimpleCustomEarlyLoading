plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev.legacyforge' version '2.0.78'
    id 'com.gradleup.shadow' version '8.3.6'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

group = 'cz.lukaskabc.minecraft.mod_loader.loading.simple_custom_early_loading'
version = mod_version
archivesBaseName = rootProject.name + "-" + jar_minecraft_version

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    shade
    implementation.extendsFrom shade
}
dependencies {
    api("org.jspecify:jspecify:1.0.0")
    shade implementation("japng:api:0.5.1")
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

// copy the final jar to run/mods directory
tasks.register('copyJar', Copy) {
    from tasks.jar.outputs.files
    into file("$rootDir/run/mods")
}

legacyForge {
    // Specify the version of Forge to use.
    version = project.minecraft_version + '-' + project.forge_version

    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            taskBefore tasks.named("copyJar")
        }

        server {
            server()
        }

        data {
            data()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }


publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}


shadowJar {
    archiveFileName = "${rootProject.name}-${project.version}.jar"
    configurations = [project.configurations.shade]

    mergeServiceFiles()

    from('LICENSE') {
        into('/')
    }
    from('NOTICE') {
        into('/')
    }
}
assemble.dependsOn('shadowJar')

tasks.named('jar', Jar).configure {
    archiveClassifier.set('lite')
    from('LICENSE') {
        into('/')
    }
    from('NOTICE') {
        into('/')
    }
}

neoFormRuntime {
    useEclipseCompiler = true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
