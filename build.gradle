plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.80'
    id 'com.gradleup.shadow' version '8.3.6'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

group = 'cz.lukaskabc.minecraft.mod_loader.loading.simple_custom_early_loading'
version = mod_version

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    shade
    implementation.extendsFrom shade
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)


neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    runs {
        configureEach {
            jvmArguments.add("-ea")
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
        }

        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }

    unitTest {
        enable()
        testedMod = mods."${mod_id}"
    }
}

dependencies {
    api("org.jspecify:jspecify:1.0.0")
    shade implementation("japng:api:0.5.1")
    clientAdditionalRuntimeClasspath("japng:api:0.5.1")

    // test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

sourceSets.main.resources { srcDir 'src/generated/resources' }


publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}


shadowJar {
    archiveFileName = "${rootProject.name}-${project.version}.jar"
    configurations = [project.configurations.shade]

    relocate 'net.ellerton.japng', 'cz.lukaskabc.minecraft.mod_loader.shaded.japng'

    mergeServiceFiles()

    from('LICENSE') {
        into('/')
    }
    from('NOTICE') {
        into('/')
    }
}
assemble.dependsOn('shadowJar')

tasks.named('jar', Jar).configure {
    archiveClassifier.set('lite')
    from('LICENSE') {
        into('/')
    }
    from('NOTICE') {
        into('/')
    }
}

neoFormRuntime {
    useEclipseCompiler = true
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
